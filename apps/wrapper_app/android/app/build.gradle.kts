plugins {
    id("com.android.application")
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id("dev.flutter.flutter-gradle-plugin")
}

val releaseStoreFile = providers.gradleProperty("WRAPPER_RELEASE_STORE_FILE").orNull
val releaseStorePassword = providers.gradleProperty("WRAPPER_RELEASE_STORE_PASSWORD").orNull
val releaseKeyAlias = providers.gradleProperty("WRAPPER_RELEASE_KEY_ALIAS").orNull
val releaseKeyPassword = providers.gradleProperty("WRAPPER_RELEASE_KEY_PASSWORD").orNull
val releaseSigningValues =
    listOf(releaseStoreFile, releaseStorePassword, releaseKeyAlias, releaseKeyPassword)
val releaseSigningConfigured = releaseSigningValues.all { !it.isNullOrBlank() }

require(releaseSigningValues.none { !it.isNullOrBlank() } || releaseSigningConfigured) {
    "Release signing requires WRAPPER_RELEASE_STORE_FILE, " +
        "WRAPPER_RELEASE_STORE_PASSWORD, WRAPPER_RELEASE_KEY_ALIAS, and " +
        "WRAPPER_RELEASE_KEY_PASSWORD."
}

android {
    namespace = "com.portnumber53.wrapper_app"
    compileSdk = flutter.compileSdkVersion
    ndkVersion = flutter.ndkVersion

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    defaultConfig {
        // TODO: Specify your own unique Application ID (https://developer.android.com/studio/build/application-id.html).
        applicationId = "com.portnumber53.wrapper_app"
        // You can update the following values to match your application needs.
        // For more information, see: https://flutter.dev/to/review-gradle-config.
        minSdk = flutter.minSdkVersion
        targetSdk = flutter.targetSdkVersion
        versionCode = flutter.versionCode
        versionName = flutter.versionName
    }

    signingConfigs {
        if (releaseSigningConfigured) {
            create("release") {
                storeFile = rootProject.file(requireNotNull(releaseStoreFile))
                storePassword = releaseStorePassword
                keyAlias = releaseKeyAlias
                keyPassword = releaseKeyPassword
            }
        }
    }

    buildTypes {
        release {
            if (releaseSigningConfigured) {
                signingConfig = signingConfigs.getByName("release")
            }
        }
    }
}

kotlin {
    compilerOptions {
        jvmTarget = org.jetbrains.kotlin.gradle.dsl.JvmTarget.JVM_17
    }
}

flutter {
    source = "../.."
}
